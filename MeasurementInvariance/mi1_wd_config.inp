TITLE:      MI withdrawn depressed;
DATA:       FILE IS wdinputfile.dat;
VARIABLE:
 NAMES ARE C5,C42,C65,C69,C75,C102,C103,C111,sex,Zage;
 USEVARIABLES ARE C5,C42,C65,C69,C75,C102,C103,C111,Zage; 
 CATEGORICAL ARE C5, C42, C65, C69, C75, C102, C103, C111;
 MISSING IS C5, C42, C65, C69, C75, C102, C103, C111 (99);
GROUPING IS sex (1 = boys 2 = girls);
ANALYSIS: PARAMETERIZATION = THETA;
ESTIMATOR = WLSMV;
ITERATIONS=50000;
!................................................................................;
!................................................................................;
! STEPS
! FIRST MODEL, Configural invariance
!   Thresholds:
!       Threshold 1 constrained
!       One 1 item per latent factor constrained
!   Factor loadings = free across groups
!   Factor means = zero in refgroup, free in other
!   Factor variance = fixed@1 
!   Correlating residual = constrained;
!   Residuals = fixed@1 in refgroup, free in other;
 

MODEL: ! FIRST
F1 BY C5@1,C42*.8,C65*.5,C69*1,C75*.8,C102*.6,C103*.5,C111*1.5;

[C5$1*-.2];[C42$1*0];  [C65$1*.5]; [C69$1*-.5];[C75$1*.3]; [C102$1*.7]; 
[C5$2*1];  [C42$2*1.5];[C65$2*1.5];[C69$2*.5]; [C75$2*1.5];[C102$2*1.5];
!
[C103$1*0];[C111$1*.5];
[C103$2*1];[C111$2*2.5];

!fix residual variances @1;
C5@1;C42@1;C65@1;C69@1;C75@1;C102@1;C103@1;C111@1;
!
!Factor mean;
[F1@0];
!Factor variance;
F1*1;

C5 ON Zage*.05;
C42 ON Zage*.05;
C65 ON Zage*.05;!
C69 ON Zage*.05;
C75 ON Zage*.05;
C102 ON Zage*.2;
C103 ON Zage*.2;
C111 ON Zage*.2;

!Correlating residuals
!C65 WITH C69*.4(201);   
!C5 WITH C75*-.2(202);   

!................................................................................;
!................................................................................;

MODEL girls:

!Factor loadings;
 F1 BY C5@1,C42*.8,C65*.5,C69*1,C75*.8,C102*.6,C103*.5,C111*1.5;

! Thresholds;
! item 69 is refitem
! 2nd threshold rather than first are constrained
[C5$1*-.2]; [C42$1*0];  [C65$1*.5];[C75$1*.3]; [C102$1*.7]; 
[C103$1*0];[C111$1*.5];

!fix residual variances @1;
 C5*1;C42*1;C65*1;C69*1;C75*1;C102*1;C103*1;C111*1

!Factor mean;
[F1*0];
!Factor variance;
F1*1;

!Correlating residuals
!C65 WITH C69*.4(201);   
!C5 WITH C75*-.2(202);   

C5 ON Zage*.05;
C42 ON Zage*.05;
C65 ON Zage*.05;
C69 ON Zage*.05;
C75 ON Zage*.05;
C102 ON Zage*.2;
C103 ON Zage*.2;
C111 ON Zage*.2;
!................................................................................;
!................................................................................;
SAVEDATA: DIFFTEST IS wd1.dat;
OUTPUT: TECH1 RESIDUAL standardized sampstat RESIDUAL;! MODINDICES(20);